new GuiControl(PlayerListGui)
{
	profile = "GuiModelessDialogProfile";
	horizSizing = "right";
	vertSizing = "bottom";
	position = "0 0";
	extent = "640 480";
	minExtent = "8 8";
	visible = 1;
	helpTag = 0;
	noCursor = 1;

	new GuiBitmapCtrl()
	{
		profile = "GuiDefaultProfile";
		horizSizing = "right";
		vertSizing = "center";
		position = "5 42";
		extent = "156 396";
		minExtent = "8 8";
		visible = 1;
		bitmap = "./hudfill.png";
		wrap = 0;
		helpTag = 0;

		new GuiTextCtrl()
		{
			profile = "HUDChatTextProfile";
			horizSizing = "center";
			vertSizing = "bottom";
			position = "45 2";
			extent = "65 20";
			minExtent = "8 8";
			visible = 1;
			text = "Score Board";
			maxLength = 255;
			helpTag = 0;
		};
		new GuiTextListCtrl(PlayerListGuiList)
		{
			profile = "HUDChatTextProfile";
			horizSizing = "width";
			vertSizing = "height";
			position = "0 22";
			extent = "134 16";
			minExtent = "8 8";
			visible = 1;
			enumerate = 0;
			resizeCell = 1;
			columns = "0 120";
			fitParentWidth = 0;
			clipColumnText = 0;
			helpTag = 0;
		};
	};
};
function PlayerListGui::update(%this, %clientId, %name, %isSuperAdmin, %isAdmin, %isAI, %score)
{
	%tag = %isSuperAdmin ? "[Super]" : (%isAdmin ? "[Admin]" : (%isAI ? "[Bot]" : ""));
	%text = StripMLControlChars(%name) SPC %tag TAB %score;
	if (PlayerListGuiList.getRowNumById(%clientId) == -1)
	{
		PlayerListGuiList.addRow(%clientId, %text);
	}
	else
	{
		PlayerListGuiList.setRowById(%clientId, %text);
	}
	PlayerListGuiList.sortNumerical(1);
}

function PlayerListGui::updateScore(%this, %clientId, %score)
{
	%text = PlayerListGuiList.getRowTextById(%clientId);
	%text = setField(%text, 1, %score);
	PlayerListGuiList.setRowById(%clientId, %text);
	PlayerListGuiList.sortNumerical(1);
}

function PlayerListGui::remove(%this, %clientId)
{
	PlayerListGuiList.removeRowById(%clientId);
}

function PlayerListGui::toggle(%this)
{
	if (%this.isAwake())
	{
		Canvas.popDialog(%this);
	}
	else
	{
		Canvas.pushDialog(%this);
	}
}

function PlayerListGui::clear(%this)
{
	PlayerListGuiList.clear();
}

